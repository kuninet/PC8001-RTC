10 ' RTC7301DG CONTROL
20 DEFFNA(X)=(X AND &HF)+ASC("0")
30 '
40 OUT &H8F,2:IF (INP(&H81)AND&H8)=8 THEN GOSUB 1000
50 PRINT "## CURRENT DATE/TIME ##"
60 GOSUB 3000
70 PRINT:GOSUB 2000
80 END
1000 'RTC7301DG INIT
1010 PRINT "RTC INIT START"
1020 OUT &H8F,2:OUT &H8E,0
1030 A=A
1040 OUT &H8F,&H6  ' SEL BANK1
1050 OUT &H8E,&H0
1060 OUT &H8B,&H4
1070 OUT &H8C,&H0
1080 OUT &H8D,&H8
1090 OUT &H8F,&HA  ' SEL BANK2
1100 OUT &H81,&H0
1110 OUT &H83,&H0
1120 OUT &H85,&H0
1130 OUT &H86,&H0
1140 OUT &H88,&H0
1150 OUT &H8F,&H2
1160 PRINT "RTC INIT END"
1170 RETURN
2000 'DATE TIME SET
2010 DT$=""
2020 INPUT "SET DATE/TIME? (YYYYMMDDyHHMMSS)";DT$
2030 IF LEN(DT$)=0 THEN RETURN
2040 IF LEN(DT$)<>15 THEN PRINT "** ERROR **":GOTO 2020
2050 PO=1:AD=&H8E
2060 FOR I=1 TO 15
2070 DD$=MID$(DT$,PO,1):DA=ASC(DD$)-ASC("0"):OUT AD,DA
2080 PO=PO+1:AD=AD-1:NEXT I
2090 PRINT "DATE TIME SET!"
2100 RETURN
3000 'DATE TIME READ
3010 OUT &H8F,0 ' BANK0 SEL
3020 DT$="":TM$=""
3030 FOR I=&H8E TO &H8B STEP -1:DT$=DT$+CHR$(FNA(INP(I))):NEXT I:DT$=DT$+"/"
3040 FOR I=&H8A TO &H89 STEP -1:DT$=DT$+CHR$(FNA(INP(I))):NEXT I:DT$=DT$+"/"
3050 FOR I=&H88 TO &H87 STEP -1:DT$=DT$+CHR$(FNA(INP(I))):NEXT I
3060 PRINT DT$;" ";:DATE$=RIGHT$(DT$,8)
3070 YO$="SUN MON TUE WED THU FRI SAT "
3080 PRINT MID$(YO$,(INP(&H86)AND&HF)*4+1,4);
3090 FOR I=&H85 TO &H84 STEP -1:TM$=TM$+CHR$(FNA(INP(I))):NEXT I:TM$=TM$+":"
3100 FOR I=&H83 TO &H82 STEP -1:TM$=TM$+CHR$(FNA(INP(I))):NEXT I:TM$=TM$+":"
3110 FOR I=&H81 TO &H80 STEP -1:TM$=TM$+CHR$(FNA(INP(I))):NEXT I
3120 PRINT TM$:TIME$=TM$
3130 RETURN
